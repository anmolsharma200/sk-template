plugins {
    id("io.micronaut.application") version "4.6.1"
    id("io.micronaut.aot") version "4.6.1"
    id("com.gradleup.shadow") version "8.3.9"
}

dependencies {
    implementation(project(":utilities"))
    implementation(project(":schema"))
    implementation(project(":security"))
    implementation(project(":api"))
    implementation(project(":spi"))

    implementation("io.micronaut.tracing:micronaut-tracing-annotation")
    implementation("io.micronaut.tracing:micronaut-tracing-opentelemetry")
    implementation("io.micronaut.tracing:micronaut-tracing-opentelemetry-http")
    implementation("io.opentelemetry:opentelemetry-extension-trace-propagators")
    implementation("io.opentelemetry:opentelemetry-exporter-otlp")

    annotationProcessor("io.micronaut:micronaut-http-validation")
    annotationProcessor("io.micronaut.openapi:micronaut-openapi")
    annotationProcessor("io.micronaut.serde:micronaut-serde-processor")

    implementation("io.micronaut.serde:micronaut-serde-jackson")
    implementation("io.micronaut:micronaut-runtime")
    implementation("ch.qos.logback:logback-classic")

    implementation("io.micronaut.security:micronaut-security")

    runtimeOnly("ch.qos.logback:logback-classic")
    runtimeOnly("org.yaml:snakeyaml")

    compileOnly("io.micronaut.openapi:micronaut-openapi-annotations")
    compileOnly("jakarta.persistence:jakarta.persistence-api:3.1.0")

    // TODO: EJS: Database
    runtimeOnly("org.postgresql:postgresql")
    implementation("io.micronaut.data:micronaut-data-jdbc")
    implementation("io.micronaut.sql:micronaut-jdbc-hikari")

    testImplementation("io.micronaut:micronaut-http-client")
    testImplementation("org.apache.commons:commons-compress:1.27.1")

    testImplementation("org.testcontainers:testcontainers-bom")
    testImplementation("org.testcontainers:junit-jupiter")
    testImplementation("org.testcontainers:testcontainers")
    testImplementation("org.testcontainers:postgresql")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

application {
    mainClass = "<%= appPackage %>.Application"
}

java {
    sourceCompatibility = JavaVersion.toVersion("21")
    targetCompatibility = JavaVersion.toVersion("21")
}

graalvmNative.toolchainDetection = false

micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("<%= appPackage %>..*")
    }
    aot {
        // Please review carefully the optimizations enabled below
        // Check https://micronaut-projects.github.io/micronaut-aot/latest/guide/ for more details
        optimizeServiceLoading = false
        convertYamlToJava = false
        precomputeOperations = true
        cacheEnvironment = true
        optimizeClassLoading = true
        deduceEnvironment = true
        optimizeNetty = true
        replaceLogbackXml = true
    }
}

tasks.withType<JavaCompile> {
    options.isFork = true
    options.compilerArgs.add("-parameters") // still good

    options.forkOptions.jvmArgs = listOf(
        "-Dmicronaut.openapi.views.spec=scalar.enabled=true",
        "-Dmicronaut.openapi.additional.files=" +
                "project:../api/build/classes/java/main/META-INF/swagger," +
                "project:../security/build/classes/java/main/META-INF/swagger",
        "-Dmicronaut.openapi.additional.include-patterns=**/*.yml",
        "-Dmicronaut.openapi.additional.files-merge-mode=append",
    )
}

tasks.named<io.micronaut.gradle.docker.NativeImageDockerfile>("dockerfileNative") {
    jdkVersion = "21"
}

<% if (generateUI) { %>
val installNpmPackages = task<Exec>("installNpmPackages") {
    workingDir("../ui")
    commandLine("npm", "install")
}

val buildReactApp = task<Exec>("buildReactApp") {
    dependsOn(installNpmPackages)

    workingDir("../ui")
    commandLine("npx", "vite", "build", "--base=/ui/")
}

val cleanReactApp = task<Delete>("cleanReactApp") {
    delete("./src/main/resources/dist")
}

val copyReactApp = task<Copy>("copyReactApp") {
    dependsOn(buildReactApp)

    from("../ui/dist")
    into("./src/main/resources/dist")
}

tasks.inspectRuntimeClasspath {
    mustRunAfter(copyReactApp)
}

// Only build/copy the UI when explicitly requested with -PbuildUi=true
tasks.processResources {
    dependsOn(copyReactApp)
}

tasks.clean {
    finalizedBy(cleanReactApp)
}
<% } %>